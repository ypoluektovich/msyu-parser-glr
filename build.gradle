plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.2'
}

group 'org.msyu.parser'
ext.pkgname = 'glr'
version '0.2.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/ypoluektovich/msyu" }
}

dependencies {
    compile 'org.msyu.javautil:cf:0.1.+'

    testCompile 'org.testng:testng:6.9.+'
    testCompile 'org.hamcrest:java-hamcrest:2.0.+'
    testCompile('org.mockito:mockito-core:1.10.+') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
}

test {
    useTestNG()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifactId pkgname

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['main']
    dryRun = false
    publish = false
    pkg {
        userOrg = 'ypoluektovich'
        repo = 'msyu'
        name = 'msyu-parser-' + project.name
        desc = 'A GLR parser implementation'
        websiteUrl = 'https://github.com/ypoluektovich/msyu-parser-glr'
        issueTrackerUrl = 'https://github.com/ypoluektovich/msyu-parser-glr/issues'
        vcsUrl = 'https://github.com/ypoluektovich/msyu-parser-glr.git'
        licenses = ['MPL-2.0']
        publicDownloadNumbers = true
        version {
            name = project.version
            vcsTag = 'v' + project.version
        }
    }
}
